---
title: "Download the data"
---

The purpose of this notebook is to download occurrences from GBIF, as well as the predictors from WorldClim2. Everything is then saved to either a stacked geotiff (layers for visualisation) or to a `JLD` serialized object (raw data for future steps in the pipeline).

## Loading the required package

```{julia}
include(joinpath("code", "pkg.jl"))
```

## Bioclimatic data

get the data from worldclim2 and clip

```{julia}
provider = RasterData(WorldClim2, BioClim)
opts = (resolution=2.5, )
boundingbox = (bottom=41.0, right=-59.5, left=-80.00, top=52.0)
temperature = SimpleSDMPredictor(provider, layer=1; opts..., boundingbox...)
```

water mask

```{julia}
water = SimpleSDMPredictor(RasterData(EarthEnv, LandCover), layer=12; boundingbox...)
land = replace(coarsen(water, minimum, (5, 5)).<100, false => nothing)
```

```{julia}
fig = Figure(; resolution=(900, 500))
ax = Axis(fig[1,1]; xlabel="Longitude", ylabel="Latitude", aspect=DataAspect())
hm = heatmap!(ax, mask(land, temperature), colormap=:thermal)
Colorbar(fig[1,2], hm; tellheight=false)
current_figure()
```

## Getting occurrence data

get some GBIF data

```{julia}
critter = taxon("Procyon lotor"; strict=false)
query = [
    "occurrenceStatus" => "PRESENT",
    "hasCoordinate" => true,
    "decimalLatitude" => (boundingbox.bottom, boundingbox.top),
    "decimalLongitude" => (boundingbox.left, boundingbox.right),
    "limit" => 300,
]
observations = occurrences(critter, query...)
```

get a few more pages of observations

```{julia}
for _ in 1:20
    occurrences!(observations)
end
```

get the plot all nice

```{julia}
scatter!(ax, observations; color=:black, marker=:cross, markersize=5)
current_figure()
```

we add a phylopic image because we *can*

```{julia}
phylopic_uuid = Phylopic.imagesof(critter; items = 1)
silhouette = phylopic_uuid |>
    Phylopic.thumbnail |>
    Downloads.download |>
    Images.load

@info Phylopic.attribution(phylopic_uuid)

silhouette_size = Vec2f(reverse(size(silhouette) ./ 3))

scatter!(ax, [-87], [47.9]; marker = silhouette, markersize = silhouette_size, color=:white)
current_figure()
```

## Selection of background points

we perform some spatial thinning, by making sure that there is only one observation per grid cell

```{julia}
presence_layer = mask(land, mask(temperature, observations, Bool))
```

we make a buffer for pseudo-absences -- we will sample in circles around known presences, but keep a 100km buffer with no background points

```{julia}
background = pseudoabsencemask(WithinRadius, presence_layer; distance = 200.0)
buffer = pseudoabsencemask(WithinRadius, presence_layer; distance = 20.0)
possible_background = background-buffer
```

we get the pseudo-absences now, by sampling an equal number of background points

```{julia}
absence_layer = SpeciesDistributionToolkit.sample(possible_background, round(Int, 0.4*sum(presence_layer)))
```

we remove the info we do not need from the layers

```{julia}
replace!(absence_layer, false => nothing)
replace!(presence_layer, false => nothing)
```

## Saving data to disk

now we need to get the full series of layers for the suite of bioclim variables

```{julia}
bioclim_clipped = [mask(land, SimpleSDMPredictor(provider; layer = l, opts..., boundingbox...)) for l in layers(provider)]
SpeciesDistributionToolkit._write_geotiff("artifacts/layers.tiff", bioclim_clipped)
```

now we bring everything into a single matrix

```{julia}
Xpresence = hcat([bioclim_var[keys(presence_layer)] for bioclim_var in bioclim_clipped]...)
ypresence = fill(true, length(presence_layer))
Xabsence = hcat([bioclim_var[keys(absence_layer)] for bioclim_var in bioclim_clipped]...)
yabsence = fill(false, length(absence_layer))
X = vcat(Xpresence, Xabsence)
y = vcat(ypresence, yabsence)
```

keep track of what the layers are 

```{julia}
bclay = layers(RasterData(WorldClim2, BioClim))
bcdes = layerdescriptions(RasterData(WorldClim2, BioClim))
```

and we save as a JLD file

```{julia}
presences = Tuple.(keys(presence_layer))
absences = Tuple.(keys(absence_layer))
variables = [(bc, bcdes[bc]) for bc in bclay]
JLD.@save "artifacts/data.jld" X y presences absences variables
```